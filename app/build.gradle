plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

// Reads the Google maps key that is used in the AndroidManifest
Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}

android {
    namespace 'id.android.basics.compose'
    compileSdk 32
    buildToolsVersion "32.0.0"

    defaultConfig {
        applicationId "id.android.basics.compose"
        minSdk 30
        targetSdk 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "id.android.basics.compose.CustomTestRunner"

        vectorDrawables {
            useSupportLibrary true
        }

        manifestPlaceholders = [googleMapsKey: properties.getProperty("google.maps.key", "")]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        resValues false
        shaders false
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation 'com.google.accompanist:accompanist-insets:0.25.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.compose.runtime:runtime:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.foundation:foundation:$compose_ui_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_ui_version"
    implementation "androidx.compose.animation:animation:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'io.coil-kt:coil-compose:2.2.2'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    implementation 'com.google.maps.android:maps-v3-ktx:3.4.0'
    constraints {
        // Volley is a transitive dependency of maps
        implementation ('com.android.volley:volley:1.2.1') {
            because("Only volley 1.2.0 or newer are available on maven.google.com")
        }
    }

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "com.google.dagger:hilt-android:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"
}